###
###
###
### Category1 : 
### Category2 : 
### Category3 : 
### Description : 
###
###
###
Dim IE, fso, MyFile
Set fso = CreateObject("Scripting.FileSystemObject")
Set IE = CreateObject("InternetExplorer.Application")
set wshshell = wscript.CreateObject("wscript.shell")
ie.left=0
ie.top=0
ie.height=600
ie.width=800
ie.menubar=0
ie.toolbar=0
ie.addressbar=0
ie.statusbar=0
ie.navigate "file:///c:\moc\setup\mocbhtml\moccust1.htm"
ie.visible=1

'Wait for London to be available
done = 0
while done = 0
	if fso.FolderExists("\\london\setup") then
		done = 1
	else
		wscript.sleep 5000
	end if
wend

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'' Establish computer's domain name
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
set oNet = CreateObject("wscript.network")
strServer = LCase(oNet.ComputerName)
Select Case strServer
	Case "denver"     strDomain = "adatum"
	Case "brisbane"   strDomain = "fabrikam"
	Case "bonn"       strDomain = "lucernpublish"
	Case "santiago"   strDomain = "litwareinc"
	Case "singapore"  strDomain = "tailspintoys"
	Case "tunis"      strDomain = "wingtiptoys"
	Case "miami"      strDomain = "thephonecompany"
	Case "suva"       strDomain = "cpandl"
	Case "moscow"     strDomain = "adventureworks"
	Case "montevideo" strDomain = "blueyonderair"
	Case "tokyo"      strDomain = "woodgrovebank"
	Case "nairobi"    strDomain = "treyresearch"
	Case Else MsgBox "Invalid student computer name."
End Select

'Setting Auto Logon
WshShell.RegWrite "HKLM\Software\Microsoft\Windows NT\CurrentVersion\Winlogon\AutoAdminLogon", 1 ,"REG_SZ"
wshshell.Regwrite "HKLM\Software\Microsoft\Windows NT\CurrentVersion\Winlogon\DefaultUserName", "administrator", "REG_SZ"
wshshell.Regwrite "HKLM\Software\Microsoft\Windows NT\CurrentVersion\Winlogon\DefaultPassword", "P@ssw0rd", "REG_SZ"
wshshell.RegWrite "HKLM\Software\Microsoft\Windows NT\CurrentVersion\Winlogon\DefaultDomainName", strDomain, "REG_SZ"

'set moctwob.vbs to run after reboot
if fso.FileExists ("c:\moc\setup\moctwob.vbs") then
	set strt = fso.getfolder("c:\documents and settings\all users\start menu\programs\startup")
	fn = "runnext.vbs"
	set nxt = fso.getfile("c:\moc\setup\moctwob.vbs")
	set fl = fso.createtextfile(strt & "\" & fn, true)
	fl.writeline("set wshshell = wscript.createobject" & chr(40) & chr(34) & "wscript.shell" & chr(34) & chr(41))
	fl.writeline("wshshell.run " & chr(34) & nxt & chr(34))
	fl.Close
else
	set MyFile = fso.GetFile("C:\Documents and Settings\All Users\Start Menu\Programs\Startup\runnext.vbs")
	MyFile.Delete
end if

'Delete the startup file
if fso.FileExists ("C:\Documents and Settings\All Users\Start Menu\Programs\Startup\mocsetup.cmd") then
	Set MyFile = fso.GetFile("C:\Documents and Settings\All Users\Start Menu\Programs\Startup\mocsetup.cmd")
	MyFile.Delete
end if

'view hidden files
wshshell.Run "c:\moc\setup\explrcfg.vbs", 6, true

'Disable Internet Connection Wizard
WshShell.run "c:\moc\setup\ie.vbs", 6, true

'Format drive D
ie.Navigate "file:///c:\moc\setup\mocbhtml\moccust1.htm"
wshshell.Run "c:\moc\setup\convert.vbs", 6, true

'Reboot the machine
WshShell.run "c:\tools\reboot.cmd", 6, true

