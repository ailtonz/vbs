###
###
###
### Category1 : 
### Category2 : 
### Category3 : 
### Description : 
###
###
###
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Script to move users from one OU to anotherin the Active Directory
' 
' This script modifies attributes from the file called change.txt
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
wscript.echo "Script starting. Please click OK to continue."

DIM arrRecord
Const ForReading = 1

''''''''''''''''''''''''''''''''''''''''''
' Determine the LDAP path for your domain
' Nothing in this section needs to be customized
''''''''''''''''''''''''''''''''''''''''''
Set Root = GetObject("LDAP://RootDSE")
DomainPath = Root.Get("DefaultNamingContext")

''''''''''''''''''''''''''''''''''''''''
' Get the pointer to your domain object
' Nothing in this section needs to be customized
''''''''''''''''''''''''''''''''''''''''
Set Domain = GetObject("LDAP://" & DomainPath)


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' The file system object is your entry point into the file system
' Nothing in this section needs to be customized
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
set fso = CreateObject ("Scripting.FileSystemObject")

''''''''''''''''''''''''''''''''''''''''''''''''''''
' Get the object to the C:\ADSI\change.txt text file
' Name and location of the text file can be altered
''''''''''''''''''''''''''''''''''''''''''''''''''''
Set tsTextFile = fso.OpenTextFile ("C:\ADSI\change.txt", ForReading, False)

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Get the object to the Human Resources OU
' You would need to customize the OU name/path
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Set ou_Current = GetObject("LDAP://OU=Human Resources, " & DomainPath)

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Begin reading the input file and loop until you reach EOF
' Read a line in the file
' Nothing in this section needs to be customized
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

While Not tsTextFile.AtEndOfStream    ' START OF LOOP

 strRecord = tsTextFile.ReadLine

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Parse the line into string segments using the colon as a delimiter
'
' (You can change the delimiter.)
'
' Put each string segment into an array named arrRecord
'
' The first string segment is referenced as arrRecord(0)
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
 arrRecord = Split (strRecord, ":")
 
'''''''''''''''''''''''''''''''''''
' Find the user in the OU
' Note that the username is the first (0) field
' Nothing in this section needs to be customized
'''''''''''''''''''''''''''''''''''
 Set adsUser = ou_Current.GetObject("user", "CN=" & arrRecord(0))

'''''''''''''''''''''''''''''''
' Overwrite the old title with the new title
'
' You may alter the attribute being overwritten
'
' Note that you can also get that attribute
' from any field in the record or add several similar
' lines in this section to change several attributes.
' In this case, Title is retrieved from the second (1) field
'''''''''''''''''''''''''''''''
 
 adsUser.Title = arrRecord (1)
 
'''''''''''''''''''''''''''''''''''''''''''''''''
' SetInfo flushes the attributes in cache to disk
' Reset adsUser in preparation for the next user object
' Close the text file
'
' Nothing in this section needs to be customized
'''''''''''''''''''''''''''''''''''''''''''''''''
 adsUser.SetInfo

 Set adsUser = Nothing
Wend                                   ' END OF LOOP

tsTextFile.Close

wscript.echo "Users modified.", Chr(10),Chr(10),"Please click OK to finish."

